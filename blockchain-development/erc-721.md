# ERC-721

* EIP-721에서 논의되어 생성된 표준(Standard)
  * EIP(Ethereum Improvement Proposal)
    * 이더리움의 핵심 프로토콜 사양, 클라이언트 API 및 계약 표준을 포함하여 이더리움(Ethereum) 플랫폼의 표준을 설명
* NFT(Non-Fungible Token)를 생성할 수 있는 표준

## NFT

* NFT(Non-Fungible Token)
* 완전히 대체 가능한 토큰이 아니라 단 하나의 토큰으로써 식별이 가능하다.
* 하나의 토큰을 추적할 수 있게 하는 토큰
* NFT를 사용한 예
  * Decentraland의 LAND, CryptoPunks, DMarket의 EnjinCoin처럼 시스템을 사용하는 게임 내 아이템
  * Ubitquity나 Propy 같은 회사에서 구상한 대로, 실제 부동산과 같은 자산을 추적하는 데에 사용
  * NFT가 게임에 사용된 대표적인 예
    * 크립토키티(CryptoKitties)
      * 크립토키티의 고양이들은 제 각각 다른 생김새를 가지고 있다.
      * 사용자가 보유하고 있는 고양이는 전 세계에서 단 하나밖에 없는 유일한 고양이가 된다. 
      * 수집형 토큰이라는 ERC-721의 특징을 활용
      * 사용자들은 이 세상에서 하나밖에 없는 유일한 고양이를 수집하는 것에서 재미를 얻는다.
* ERC-721은 토큰이 디앱 내에서 공유될 수 있고, 미래 NFT 생성에 영향을 미칠 수 있는 수집물이 될 수 있도록 한다.
* NFT는 무제한으로 공급이 가능하지만, 가치의 손상을 막기 위해 대부분의 디앱 프로젝트가 NFT의 전체 공급량을 제한하고 있다.
* NFT라는 용어를 대신해서 사용할 용어로 Distinguishable Asset, Title Token, Asset, Equity, Ticket 등이 고려되었다.
* minting
  * 민팅
  * 블록체인 자산을 생성하는 과정

### ERC-20 토큰과 ERC-721의 차이점

* ERC-20 토큰
  * 2017년 당시 가장 많이 쓰이고 있던 토큰
  * 일반적으로 거래소에서 거래하고 있는 대부분의 토큰 발행 기준
  * ERC-20 토큰이 대체 가능한 암호화폐를 지원
    * 일반적인 화폐와 같다.
    * 누구나 가질 수 있고, 같은 가치를 지닌다.
* ERC-721 토큰 
  * ERC-20 토큰과는 반대로 '대체 불가능'한 특징
  * RC-721로 발행되는 토큰은 모두 각각의 가치를 가지고 있다.
    * 크립토키티
      * 모두 다 다른 고양이가 만들어진다.
      * 더 예쁜 고양이는 시장에서 높은 가치를 받고 팔리고 별로인 고양이는 팔리지 않을 수도 있을 만큼 가치가 없다.

## ERC-721 함수의 기능

* ERC721은 옵션 확장이 여러 개 있는 ERC20보다 더 복잡한 표준이며 여러 계약으로 분할된다.
* ERC-20과 겹치는 함수
  * balanceOf
    * 해당 owner가 가진 NFT의 갯수 반환
  * approve
    * 특정 계정에게 자신이 소유한 NFT하나를 사용하도록 허용
  * transferFrom
    * NFT 소유권 전송
    * token ID를 송신자에서 수신자로 전송한다.
    * 사용 권장하지 않는다.
  * safeTransferFrom(from, to, tokenId)
    * 받는 주소가 NFT를 받을 수 있는지 확인 후 NFT 소유권 전송
      * 받을 수 없다면 함수 실행을 취소한다.
    * ERC165 표준 사용
    * safeTransferFrom(from, to, tokenId, data)
      * data
        * NFT를 받을 수 있는지 구현 여부 확인

### GameItem에서 NFT 사용하기 - awardItem

* ERC721을 사용하여, 각각 고유한 속성이 있는 게임 아이템을 생성할 수 있다.
* 플레이어에게 보상될 때마다 새로운 NFT가 발행되어 플레이어에게 전송된다.
* 플레이어는 블록체인의 다른 자산과 마찬가지로 토큰을 자유롭게 유지할 수 있고, 다른 사람들과 거래할 수 있다.
* 모든 플레이어의 계정에서 awardItem 을 통해 아이템 생성을 호출 할 수 있다.
* 어떤 계정이 아이템을 만들 수 있는지 제한하기 위해 Access Control 을 추가할 수도 있다.

```sol
// contracts/GameItem.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "@openzeppelin/contracts/utils/Counters.sol";

contract GameItem is ERC721 {
    using Counters for Counters.Counter;
    Counters.Counter private _tokenIds;

    constructor() public ERC721("GameItem", "ITM") {}

    function awardItem(address player, string memory tokenURI)
        public
        returns (uint256)
    {
        _tokenIds.increment();

        uint256 newItemId = _tokenIds.current();
        _mint(player, newItemId);
        _setTokenURI(newItemId, tokenURI);

        return newItemId;
    }
}
```

### ApprovalForAll(address owner, address operator, bool approved)

* 컨트랙트 오너(owner)가 오퍼레이터(operator)에게 모든 자산을 관리할 수 있는 권한을 부여하거나 없앨 수 있다.
* `ApprovalForAll` 은 `Transfer(from, to, tokenId)`, `Approval(owner, approved, tokenId)` 와 함께 함수가 아닌 이벤트로 취급된다.
  * `setApprovalForAll` 함수를 실행하면, 이 이벤트가 발생한다.

### ownerOf(uint256 tokenId) → address owner

* 모든 NFT는 고유한 tokenId가 있다.
* `tokenId` 가 반드시 존재해야 하며, `tokenId` 에 해당하는 NFT 소유자 주소를 리턴한다.
  * 리턴값이 있는 함수의 경우, 다음과 같이 표기
  * 함수이름(변수자료형 변수이름) → (리턴값) 변수자료형 변수이름
* 함수 ownerOf는 public으로 공개해 사용
* tokenId 를 입력받고, 해당 토큰을 소유하고 있는 주소를 리턴
* 토큰의 소유자가 없다면(오너의 주소값이 0이라면), 에러를 발생

```sol
function ownerOf(uint256 tokenId) public view virtual override returns (address) {
        address owner = _owners[tokenId];
        require(owner != address(0), "ERC721: owner query for nonexistent token");
        return owner;
    }
```

### setApprovalForAll(address operator, bool _approved)

* 특정 계정에게 자신이 소유한 모든 NFT에 대한 사용 허용
* 오퍼레이터(operator)의 모든 자산을 관리할 수 있는 권한을 부여하거나 없앨 수 있다.
* 함수를 실행하면, `ApprovalForAll(owner, operator, approved)` 이벤트가 발생
* 오퍼레이터는 이 함수를 호출할 수 없다.
  
```sol
function setApprovalForAll(address operator, bool approved) public virtual override {
        _setApprovalForAll(_msgSender(), operator, approved);
    }
``` 

* `setApprovalForAll` 함수는 위와 같이 public으로 공개해 사용
  * 함수 내부에서는 전달받은 파라미터 `operator` 와 `approved` 를 함수 `_setApprovalForAll` 로 전달
  * 함수 `_setApprovalForAll` 은 함수 내부에서만 사용가능한 internal 함수이다.

```sol
/**
     * @dev Approve `operator` to operate on all of `owner` tokens
     *
     * Emits a {ApprovalForAll} event.
     */
function _setApprovalForAll(
    address owner,
    address operator,
    bool approved
) internal virtual {
    require(owner != operator, "ERC721: approve to caller");
    _operatorApprovals[owner][operator] = approved;
    // ApprovalForAll(owner, operator, approved) 이벤트를 발생시킵니다.
    emit ApprovalForAll(owner, operator, approved);
}
```

  * `require` 구문을 통해, owner와 operator가 서로 다른지 검사
    * 오너가 자기 자신을 제외한 다른 주소에게 권한을 주는 함수
    * require를 통해 검증 후 다음 코드로 넘어간다.
  * `_operatorApprovals` 객체는 오너와 오퍼레이터를 매칭
    * 오퍼레이터에게 권한을 준상태이면 true, 그렇지 않으면 false로 상태
  * 파라미터로 전달된 approved에 따라 결정
  * 마지막으로 `emit ApprovalForAll(owner, operator, approved)` 으로 이벤트를 발생

### getApproved(uint256 tokenId) → address operator

* 특정 NFT가 다른 계정에게 사용 승인되었는지 여부 승인된 주소 반환
* `tokenId` 가 반드시 존재해야 하며, `tokenId` 에 대해 승인된 어카운트(account)를 리턴
  * `tokenId` 를 파라미터로 입력받고, `tokenId` 와 매핑된 주소(address) 값을 리턴

```sol
function getApproved(uint256 tokenId) public view virtual override returns (address) {
        require(_exists(tokenId), "ERC721: approved query for nonexistent token");

        return _tokenApprovals[tokenId];
    }
```  

### isApprovedForAll(address owner, address operator) → bool

* 오퍼레이터가 오너의 모든 자산을 관리할 수 있는 권한의 여부를 리턴
* 함수 `isApprovedForAll` 은 owner의 주소와 operator의 주소를 입력받고, owner가 operator에게 준 권한의 유무를 boolean으로 리턴

```sol
function isApprovedForAll(address owner, address operator) public view virtual override returns (bool) {
        return _operatorApprovals[owner][operator];
    }
```

## ERC-165

* ERC165는 ERC-721 스마트 컨트랙트를 생성할 때 반드시 구현해야하는 인터페이스(Interface)를 검사하고, 언제 사용하는지 감지한다.
* 인터페이스 ERC165는 함수 `supportsInterface(bytes4 interfaceID)` → `bool` 만 가지고 있다.
  * 이 함수의 파라미터로 ERC-721의 인터페이스 ID를 입력해야만, ERC-721 스마트 컨트랙트가 정상적으로 동작

```sol
// 인터페이스 ERC165

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/introspection/IERC165.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC165 standard, as defined in the
 * https://eips.ethereum.org/EIPS/eip-165[EIP].
 *
 * Implementers can declare support of contract interfaces, which can then be
 * queried by others ({ERC165Checker}).
 *
 * For an implementation, see {ERC165}.
 */
interface IERC165 {
    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30 000 gas.
     */
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}
```

* 인터페이스 ERC165를 통해 사용가능한 인터페이스인지 확인할 수 있다.
* EIP-721에 작성되어 있는 인터페이스 id는 `0x80ac58cd`이다.
  * 인터페이스 id는 ERC-721에 작성된 기본적인 인터페이스를, 몇 번의 암호화 과정을 거쳐 축약한다.
  * 인터페이스 id는 작성된 함수를 하나의 잎(leaf)으로 두고 생성한 머클 루트(root)로 볼 수 있다.
  * ERC-721의 인터페이스 id가 생성되는 예시

```sol
/*
     *     bytes4(keccak256('balanceOf(address)')) == 0x70a08231
     *     bytes4(keccak256('ownerOf(uint256)')) == 0x6352211e
     *     bytes4(keccak256('approve(address,uint256)')) == 0x095ea7b3
     *     bytes4(keccak256('getApproved(uint256)')) == 0x081812fc
     *     bytes4(keccak256('setApprovalForAll(address,bool)')) == 0xa22cb465
     *     bytes4(keccak256('isApprovedForAll(address,address)')) == 0xe985e9c
     *     bytes4(keccak256('transferFrom(address,address,uint256)')) == 0x23b872dd
     *     bytes4(keccak256('safeTransferFrom(address,address,uint256)')) == 0x42842e0e
     *     bytes4(keccak256('safeTransferFrom(address,address,uint256,bytes)')) == 0xb88d4fde
     *
     *     => 0x70a08231 ^ 0x6352211e ^ 0x095ea7b3 ^ 0x081812fc ^
     *        0xa22cb465 ^ 0xe985e9c ^ 0x23b872dd ^ 0x42842e0e ^ 0xb88d4fde == 0x80ac58cd
     */
    bytes4 private constant _INTERFACE_ID_ERC721 = 0x80ac58cd;

    constructor () public {
        // ERC165를 통한 ERC721의 확인을 위한 지원 인터페이스 등록
        _registerInterface(_INTERFACE_ID_ERC721);
    }
```

### onERC721Received(address operator, address from, uint256 tokenId, bytes data) → bytes4

* 함수 `onERC721Received` 는 컨트랙트 `IERC721Receiver` 에 작성된 함수
  * `IERC721Receiver` 는 자산의 컨트랙트에서 `safeTransfer`를 지원하려는 모든 컨트랙트에 대한 인터페이스
* NFT의 수신을 처리하는 이 컨트랙트는 `safeTransfer` 후, 수신자가 구현한 이 함수를 호출
  * 반드시 함수 선택자를 반환해야 한다.
  * 그렇지 않을 경우, 호출자의 트랜잭션은 되돌려진다.
  * 반환될 `Selector` 는 `IERC721.onERC721Received.selector` 로 얻을 수 있다.
  * OpenZeppelin에서 `onERC721Received` 예시

```sol
/**
     * @dev Internal function to invoke {IERC721Receiver-onERC721Received} on a target address.
     * The call is not executed if the target address is not a contract.
     *
     * @param from address representing the previous owner of the given token ID
     * @param to target address that will receive the tokens
     * @param tokenId uint256 ID of the token to be transferred
     * @param _data bytes optional data to send along with the call
     * @return bool whether the call correctly returned the expected magic value
     */
function _checkOnERC721Received(
    address from,
    address to,
    uint256 tokenId,
    bytes memory _data
) private returns (bool) {
    if (to.isContract()) {
        try IERC721Receiver(to).onERC721Received(_msgSender(), from, tokenId, _data) returns (bytes4 retval) {
            return retval == IERC721Receiver.onERC721Received.selector;
        } catch (bytes memory reason) {
            if (reason.length == 0) {
                revert("ERC721: transfer to non ERC721Receiver implementer");
            } else {
                assembly {
                    revert(add(32, reason), mload(reason))
                }
            }
        }
    } else {
        return true;
    }
}
```

### tokenURI(uint256 tokenId) → string

* 인터페이스 `IERC721Metadata` 에 작성되어 있다.
* `tokenId` 를 입력받아 URI(Uniform Resource Identifier)를 리턴
  * 일반적으로 NFT에 포함될 이름, 설명, 이미지 URI, Properties를 포함하는 JSON 파일의 형태를 저장한 URI를 입력
* public으로 공개해 사용
* 실제 함수 내부에서는 전달받은 파라미터 `tokenId`가 이미 존재하는지 검사
  * `tokenId`에 해당하는 NFT가 있다면, 리턴값을 준비
  * 함수`_baseURI`를 실행하여, baseURI가 설정되어 있는지 검사
  * baseURI가 설정되어 있다면, baseURI와 tokenId를 합친 문자열을 리턴
  * baseURI가 없다면 빈 문자열을 리턴

```sol
function tokenURI(uint256 tokenId) public view virtual override returns (string memory) {
        require(_exists(tokenId), "ERC721Metadata: URI query for nonexistent token");

        string memory baseURI = _baseURI();
        return bytes(baseURI).length > 0 ? string(abi.encodePacked(baseURI, tokenId.toString())) : "";
    }
```

### tokenByIndex(uint256 index) → uint256

* 인터페이스 `IERC721Enumerable`에 작성되어 있다.
* 컨트랙트에 의해 저장된 모든 토큰의 index를 기반으로 token ID를 리턴
* `totalSupply` 와 함께 사용하여 모든 토큰을 열거할 수 있다.

### tokenOfOwnerByIndex(address owner, uint256 index) → uint256 tokenId

* 인터페이스 `IERC721Enumerable` 에 작성되어 있다.
* 토큰 목록의 인덱스를 기반으로 오너가 소유한 토큰 ID를 리턴
* `balanceOf` 와 함께 사용하여 오너의 모든 토큰을 열거할 수 있다.

## Remix에서 ERC-721 개발하기

* code/MyNFTs.sol
  * name : MyNFTs
  * symbol : MNFT
  * openzeppelin 의 ERC-721만을 상속받아 생성한 컨트랙트는, NFT를 발행하는 기능이 없다.
    * `approve`, `safeTransferFrom`, `setApprovalForAll`, `transferFrom` 의 트랜잭션 생성 함수와, 몇 가지의 call 함수
    * openzeppelin 의 ERC721.sol 에는 ERC-721 표준만 구현되어 있다.
      * 한정된 수량의 토큰을 관리할 때에는 미리 발행해두고 토큰의 데이터(tokenUri)를 업데이트하는 것으로 관리할 수 있다.
      * 만약 컨트랙트를 이용해 새로운 NFT를 발행하려면, 새로운 기능을 추가해야 한다.
* `mintNFT` 트랜잭션 생성 함수가 포함된 걸 확인할 수 있다.
* 